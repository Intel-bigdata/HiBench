use etl_sales_db;

add jar lib/Hammer-java.jar;
create temporary function etl as 'com.intel.hammer.log.etl.udtf.LogParser';

create table IF NOT EXISTS web_log_fact(
	w_date_sk bigint,
	w_time_sk bigint,
	w_url string,
	w_ip_sk bigint,
	w_agent_sk bigint,
	w_agent_raw string,
	w_cookie string,
	w_retcode int,
	w_request string,
	w_item_sk bigint
)
ROW FORMAT SERDE 'org.apache.hadoop.hive.serde2.lazy.LazySimpleSerDe' 
WITH SERDEPROPERTIES ('field.delim'='|','serialization.null.format'='');

insert into table web_log_fact select etl(w_content) as (w_date_sk,w_time_sk,w_url,w_ip_sk,w_agent_sk,w_agent_raw,w_cookie,w_retcode,w_request,w_item_sk) from s_web_logs_raw;

insert overwrite table cookies select distinct c_customer_sk,c_cookie from (select c_customer_sk,c_cookie from cookies union all select c_customer_sk,c_cookie from s_cookies) tmp_cookies;
insert overwrite table ip select distinct i_ip_sk,i_ip from (select i_ip_sk,i_ip from ip union all select i_ip_sk,i_ip from s_ip) tmp_ip;
insert overwrite table useragents select distinct u_useragent_sk,u_os,u_browser from (select u_useragent_sk,u_os,u_browser from useragents union all select u_useragent_sk,u_os,u_browser from s_useragents) tmp_useragents;
